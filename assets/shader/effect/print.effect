// Copyright (c) 2017-2018 Xiamen Yaji Software Co., Ltd.  

CCEffect %{
  techniques:
  - passes:
    - vert: vs
      frag: fs
      blendState:
        targets:
        - blend: true
      rasterizerState:
        cullMode: none
      properties:
        texture: { value: white }
        alphaThreshold: { value: 0.5 }
        rows: {value : 1}
        runTime: {value : 3.0 }
        speed: {value : 200.0 }
        width: {value : 100.0 }
        isBegin: {value : false}
}%


CCProgram vs %{
  precision highp float;

  #include <cc-global>
  #include <cc-local>

  in vec3 a_position;
  in vec4 a_color;
  out vec4 v_color;

  #if USE_TEXTURE
  in vec2 a_uv0;
  out vec2 v_uv0;
  #endif

  void main () {
    vec4 pos = vec4(a_position, 1);

    #if CC_USE_MODEL
    pos = cc_matViewProj * cc_matWorld * pos;
    #else
    pos = cc_matViewProj * pos;
    #endif

    #if USE_TEXTURE
    v_uv0 = a_uv0;
    #endif

    v_color = a_color;

    gl_Position = pos;
  }
}%


CCProgram fs %{
  precision highp float;
  
  #include <alpha-test>
  #include <texture>
  #include <cc-global>
  #include <cc-local>

  in vec4 v_color;

  #if USE_TEXTURE
  in vec2 v_uv0;
  uniform sampler2D texture;

  uniform myData{
      float beginTime;
      float rows;
      bool isBegin;
  };

  #if LOCK_SPEED
    uniform mydata1{
      float speed;
      float width;
    };
  #endif

  #if LOCK_TIME
      uniform mydata2{
      float runTime;
    };
  #endif

  #endif

  void main () {
    vec4 o = vec4(1, 1, 1, 1);
    vec2 uv = v_uv0;
    vec4 color = v_color;

    #if USE_TEXTURE
      CCTexture(texture, v_uv0, o);

      if(isBegin) {
   
          #if LOCK_SPEED
              float runTime = width*rows/speed;
          #endif
          
          //根据已运行时间，判断当前总进度
          float curProgress = (cc_time.x - beginTime/1000.0)/runTime; 
          if(curProgress > 1.0) {
            curProgress = 1.0;
          }

          //根据总进度，判断当前行数 ，从1行开始
          float curRow = ceil( curProgress*rows );
             
          if(uv.y < (curRow-1.0)/rows    ) {
              //已经完全跑完了的
              color.a = 1.0;
          }else if(uv.y <= curRow/rows){
              //正在跑的
              if(uv.x <= rows * (curProgress- (curRow-1.0)/rows)) {
                color.a = 1.0;
              }else{
                color.a = 0.0;
              }
          }else{
              //还未开始跑的
              color.a = 0.0;
          }
      }else{
        color.a = 0.0;
      }

    #endif

    o *= color;

    ALPHA_TEST(o);

    gl_FragColor = o;
  }
}%
